*  Summary:
*   ^namemerge^ does a matching of two different firm databases based on name.
*   The matching is a left join, keeping all observations from the first file
*   but only the matched ones from the second file. ^namemerge^ requires that  
*   ^tomname^ has already been run on both datasets and uses the parameters 
*   created by ^tomname^ to do the matching. It additionally allows the user
*   to add other parameters to the matching criteria.
*
*   Matching is done in the following order:
*       1. Match both match_name and match_type
*       2. Match only match_name
*       3. Match match_collapsed
*
*   Requires @tomanme@ and @safedrop@

* Parameters:
*   namelist   the list of the two .dta files to merge
*   extravar   matching can add extravariables besides the ones generated by tomname
*              which are then included in all matching (e.g. state)
*   tempfolder This script generates temp files through the merging. If the files
*               are very large another path might be preferred.
*
*
* Output:
*   _mergex contains the results from this merge, which can either be a match of:
*      name and type, name only, collapsed, or no match
*

capture program drop namemerge

program define namemerge,rclass
	syntax anything, [EXTRAvars(string)] [TEMPfolder(string)] [NOSAVE] [origlocal(string)]
	
	if "`extravars'" != "" {
		di "Matching also on secondary parameterss: `extravars'"
	}
	
	set more off
	
	
	if "`tempfolder'" == "" {
		local root_folder = "/projects/reap.proj/temp"
	}
	else{
		local root_folder = "`tempfolder'"
	}
	
	capture confirm file "`root_folder'/merge1.dta"
	if _rc == 0{
		if "`safe'" == "safe"{
			di "Error. File `root_folder'/merge1.dta exists.", as error
			local _rc=1
			error `_rc'
		}
	
		di "File `root_folder'/merge1.dta exists. Dropping." 
		rm `root_folder'/merge1.dta
	}
	
	capture confirm file "`root_folder'/merge2.dta"
	if _rc == 0{
		if "`safe'" == "safe"{
			di "Error. File `root_folder'/merge2.dta exists.", as error
			local _rc=1
			error `_rc'
		}
		di "File `root_folder'/merge2.dta exists. Dropping."
		rm `root_folder'/merge2.dta
	}

	/* Load the first file */
	use `1',replace
	
	/* This is temporary change used to compare the full names
	   of the left and right files. It is later undone. */
	rename mfull_name mfull_leftname
	
	if "`safe'" == "safe"{
		foreach v on _merge _mergex {
			capture confirm var `v'
			if _rc == 0 {
				di "Error. Variable `v' exists.", as error
				local _rc=1
				error `_rc'
			}
		}
	}
	
	safedrop _merge _mergex

	/* Get the list of all vars in the left dataset so that we 
	 * only keep those after we do merge operations
	 */
	local originalvars   = ""
	foreach var of varlist _all {
		local originalvars = "`originalvars' `var'"  
	}

	keep `originalvars'
	
	/* Merge by name and type
	 */
	merge m:m match_name match_type `extravars' using `2'
	
	/* This program uses the file merge1.dta to keep all observations 
	 * that have not been matched and file merge2.dta to keep those that already have a match.	
	 */
	save `root_folder'/merge1.dta,replace
	keep if _merge ==3
	gen _mergex = "name and type"
	save `root_folder'/merge2.dta,replace

	use `root_folder'/merge1.dta,replace
	keep if _merge == 1 //only the left observations that do not have a match
	keep `originalvars'
	
	/* Merge by name only
	 */
	merge m:m  match_name `extravars'  using `2'
	rename mfull_name mfull_rightname
	
	/* Drops cases where two firms have almost the same name but one is a corporation and the 
	 * other one an LLC. This is not uncommon. 
	 */
	replace _merge = 1 if strpos(mfull_rightname," LLC") >0 & (strpos(mfull_leftname," INCORPORATED") > 0 | strpos(mfull_leftname," CORPORATION") > 0)
	replace _merge = 1 if strpos(mfull_leftname," LLC") >0 & (strpos(mfull_rightname," INCORPORATED") > 0 | strpos(mfull_rightname," CORPORATION") > 0)

	save `root_folder'/merge1.dta,replace
	keep if _merge == 3 
	gen _mergex = "only name" 
	append using `root_folder'/merge2.dta //add to the previously matched observations
	save `root_folder'/merge2.dta,replace //and store them 

	use `root_folder'/merge1.dta,replace
	keep if _merge == 1
	keep `originalvars'
	merge m:m  match_collapsed `extravars'  using `2'
	keep if _merge == 3 | _merge == 1
	gen _mergex = "collapsed name" if _merge == 3
	replace _mergex = "no match" if _merge == 1
	append using `root_folder'/merge2.dta

	safedrop mfull_rightname
	safedrop mfull_name

	rename mfull_leftname mfull_name


	//Drop the temp file
	rm `root_folder'/merge1.dta
	rm `root_folder'/merge2.dta
	
	if "`origlocal'" != "" {
		c_local `origlocal' = "`originalvars'"
	}

	tab _mergex
end


